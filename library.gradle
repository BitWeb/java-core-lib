apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spring-core'
            from components.java
            pom {
                name = 'Bitweb Java Core Library'
                description = 'Bitweb Spring Boot Java Core Library'
                url = 'https://github.com/BitWeb/java-core-lib'
                //properties = []
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/BitWeb/java-core-lib/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'Jorich'
                        name = 'Erich Jagomägis'
                        email = 'erich@bitweb.ee'
                    }
                    developer {
                        id = 'rammrain'
                        name = 'Rain Ramm'
                        email = 'rain@bitweb.ee'
                    }
                    developer {
                        id = 'meritlaidroo'
                        name = 'Merit Laidroo'
                        email = 'merit.laidroo@bitweb.ee'
                    }
                    developer {
                        id = 'taavisikk'
                        name = 'Taavi Sikk'
                        email = 'taavi.sikk@bitweb.ee'
                    }
                    developer {
                        id = 'ennoeller'
                        name = 'Enno Eller'
                        email = 'enno.eller@bitweb.ee'
                    }
                    developer {
                        id = 'pr11t'
                        name = 'Priit Pärkson'
                        email = 'priit.parkson@bitweb.ee'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/BitWeb/java-core-lib.git'
                    developerConnection = 'scm:git@github.com:BitWeb/java-core-lib.git'
                    url = 'https://github.com/BitWeb/java-core-lib'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        if (project.hasProperty('mavenCentralUsername') & project.hasProperty('mavenCentralPassword')) {
            sonatype {
                nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
                snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
                username = project.mavenCentralUsername
                password = project.mavenCentralPassword
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") & project.hasProperty("signingPassword")) {
        useInMemoryPgpKeys(project.signingKey, project.signingPassword)
    }
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jar {
    from sourceSets.main.allSource
}

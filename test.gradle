apply plugin: 'jacoco'

test {
    useJUnitPlatform()
}

tasks.register('unitTest', Test) {
    group = "verification"

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    useJUnitPlatform {
        excludeTags "integration"
    }
}

tasks.register('integrationTest', Test) {
    group = "verification"

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    useJUnitPlatform {
        includeTags 'integration'
    }
}

tasks.register('jacocoUnitTestReport', JacocoReport) {
    sourceSets sourceSets.main
    executionData unitTest

    reports {
        xml.required = true
    }
}

tasks.register('jacocoIntegrationTestReport', JacocoReport) {
    sourceSets sourceSets.main
    executionData integrationTest

    reports {
        xml.required = true
    }
}

tasks.register('testAndReport') {
    tasks.getByName('integrationTest').mustRunAfter('unitTest')
    dependsOn('unitTest', 'integrationTest', 'jacocoUnitTestReport', 'jacocoIntegrationTestReport')
}

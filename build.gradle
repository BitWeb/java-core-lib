buildscript {
    ext {
        springBootVersion = '2.+'
        springVersion = '5.+'
        lombokVersion = '1.18.+'
        mockitoVersion = '4.8.+'
        retrofitVersion = '2.9.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.5.3'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id "org.owasp.dependencycheck" version "6.5.3"
}

group 'ee.bitweb'
version '2.4.0'

sourceCompatibility = 11

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://nexus.bitweb.ee/repository/maven-public/"
            def snapshotsRepoUrl = "https://nexus.bitweb.ee/repository/maven-snapshot/"

            name 'nexus'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.repoUser
                password = project.repoPassword
            }
        }
    }
}

jar {
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
    maven {
        url "https://nexus.bitweb.ee/repository/maven-public/"
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.springframework/spring-webmvc
    implementation group: 'org.springframework', name: 'spring-webmvc', version: "${springVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-configuration-processor
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-actuator
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    // https://mvnrepository.com/artifact/org.springframework/spring-tx
    implementation group: 'org.springframework', name: 'spring-tx', version: "${springVersion}"

    // https://mvnrepository.com/artifact/io.swagger/swagger-annotations
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.6'

    // https://mvnrepository.com/artifact/io.springfox/springfox-spring-web
    implementation group: 'io.springfox', name: 'springfox-spring-web', version: '2.10.+'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.13.4'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.module/jackson-module-kotlin
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.13.4'

    // https://mvnrepository.com/artifact/io.springfox/springfox-bean-validators
    implementation group: 'io.springfox', name: 'springfox-bean-validators', version: '2.10.+'

    // https://mvnrepository.com/artifact/javax.validation/validation-api
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-autoconfigure
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: "${retrofitVersion}"

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-jackson
    implementation group: 'com.squareup.retrofit2', name: 'converter-jackson', version: "${retrofitVersion}"

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.10.0'

    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-web
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "${springBootVersion}"

    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"

    // https://mvnrepository.com/artifact/org.json/json
    testImplementation group: 'org.json', name: 'json', version: '20220320'

    // https://bitbucket.org/bitwebou/java-core-test-lib/src/master/
    testImplementation group: 'ee.bitweb', name: 'test-core-lib', version: '1.+'

    // https://mvnrepository.com/artifact/org.mockito/mockito-inline
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: "${mockitoVersion}"

    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"

    // https://mvnrepository.com/artifact/org.mock-server/mockserver-netty
    testImplementation ("org.mock-server:mockserver-netty:5.14.0") {
        exclude group: 'junit', module: 'junit'
    }
}

task copyDependencies(type: Copy) {
    from configurations.annotationProcessor
    into './build/dependencies'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    useJUnitPlatform()
}

task unitTest(type: Test) {
    group("verification")

    useJUnitPlatform {
        excludeTags "integration"
    }
}

task integrationTest(type: Test) {
    group("verification")

    useJUnitPlatform {
        includeTags 'integration'
    }
}

task jacocoUnitTestReport(type: JacocoReport) {
    sourceSets sourceSets.main
    executionData unitTest

    reports {
        xml.enabled true
    }
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    sourceSets sourceSets.main
    executionData integrationTest

    reports {
        xml.enabled true
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testCompileOnly {
        extendsFrom testAnnotationProcessor
    }
    deployerJars
}
